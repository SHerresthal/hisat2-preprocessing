import yaml

include: "/scripts/snakemake-scripts.py"

configfile: "/config/config.yml"

fastqdir = "/fastq"
samples = get_samples(fastqdir)
config = parse_config(config, samples)

# "/meta/hisat2"
indexdir = str(Path(config['metadir'])/"hisat2")
# "/meta/genome.fasta"
fastafile = str(Path(config['metadir'])/config['fasta'])
# "genome"
indexname = str(Path(config['fasta']).stem)
# "/meta/hisat2/genome"
indexstem = str(Path(indexdir)/indexname)
# "/meta/annotation.gtf"
gtffile = str(Path(config['metadir'])/config['gtf'])

indexfiles = expand(
    indexstem+".{i}.ht2",
    i=range(1,9))


rule all:
    input:
        "config.yml",
        indexfiles,
        # count vectors
        expand("counts/{sample}.tsv", sample=samples.keys()),
        # qc from htseq_qc
        expand("qc/htseq_qc/{sample}.pdf", sample=samples.keys()),
        # multiqc outputs
        "qc/multiqc/summary.html"


# writes the updated config file to the output directory
rule storeconfig:
    output:
        "config.yml"
    run:
        with open(output[0],"w") as f:
            yaml.dump(config, f, default_flow_style=False)


rule clean:
    shell:
        "rm -rf /output/* /index/*.gff /index/hisat2/*"


rule samtools_sort:
    input:
        "mapped/unsorted/{sample}.bam"
    output:
        "mapped/sorted/{sample}.bam"
    params:
        "-m 4G"
    threads: 8
    wrapper:
        "0.2.0/bio/samtools/sort"


rule htseq:
    input:
        bam="mapped/sorted/{sample}.bam",
        gtf=gtffile
    output:
        "counts/{sample}.tsv"
    log:
        "logs/htseq/{sample}.log"
    shell:
        """htseq-count --order=pos --format=bam \
        {input.bam} {input.gtf} > {output} 2> {log}"""


rule multiqc_summary:
    input:
        expand("counts/{sample}.tsv", sample=samples.keys()),
        expand("logs/hisat2/{sample}.log", sample=samples.keys())
    output:
        "qc/multiqc/summary.html"
    params:
        ""
    log:
        "logs/multiqc.log"
    wrapper:
        "0.27.1/bio/multiqc"


rule htseq_qa:
    input:
        "mapped/sorted/{sample}.bam",
    output:
        "qc/htseq_qc/{sample}.pdf"
    shell:
        """htseq-qa --type=bam {input} --outfile={output} --maxqual=60"""


rule hisat2index:
    input:
        fastafile
    output:
        indexfiles
    log:
        "logs/hisat2-index.log"
    shell:
        "cd " + indexdir + " && "
        "hisat2-build {input} " + indexname


rule hisat2:
    input:
        # the function below selects either single or paired inputs
        # depending on the option 'mode'
        reads=lambda wildcards: hisat2_input(wildcards, config['mode'])
    output:
        "mapped/unsorted/{sample}.bam"
    log:
        "logs/hisat2/{sample}.log"
    params:
        idx=indexstem,
        extra="--new-summary"
    threads: 8
    wrapper:
        "0.27.1/bio/hisat2"


rule merge:
    input:
        lambda wildcards: samples[wildcards.sample][wildcards.read]
    output:
        "merged/{sample}_R{read}.fastq"
    log:
        "logs/merging/{sample}_R{read}_merge.log"
    shell:
        """
        echo {input} | tr ' ' '\n' > {log};
        zcat {input} > {output}"""
